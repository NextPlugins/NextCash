plugins {
    id 'java'

    id 'com.github.johnrengelman.shadow' version '5.0.0'

    id 'net.minecrell.plugin-yml.bukkit' version '0.3.0'
}

group 'com.nextplugins'
version '1.2.7'

tasks.build.dependsOn('shadowJar')

repositories {
    mavenCentral()

    // spigot

    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://oss.sonatype.org/content/groups/public/' }

    // plugins

    maven { url = 'https://repo.codemc.org/repository/maven-public' }
    maven { url = 'https://repo.citizensnpcs.co/' }

    maven { url = 'https://jitpack.io/' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.18.1-R0.1-SNAPSHOT'

    compileOnly 'com.github.PlaceholderAPI:PlaceholderAPI:2.10.9'
    compileOnly 'com.gmail.filoghost.holographicdisplays:holographicdisplays-api:2.4.0'
    compileOnly 'net.citizensnpcs:citizens:2.0.27-SNAPSHOT'
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
    compileOnly fileTree('libs')

    implementation 'com.github.Yuhtin:update-checker:1.2'
    implementation 'com.github.HenryFabio.configuration-injector:bukkit:1.0.2'
    implementation 'com.github.SaiintBrisson.command-framework:bukkit:1.2.0'

    implementation 'commons-io:commons-io:2.11.0'
    implementation 'de.tr7zw:item-nbt-api:2.9.2'
    implementation 'com.github.HenryFabio:sql-provider:9561f20fd2'
    implementation 'com.github.HenryFabio:inventory-api:2.0.3'

    def lombok = 'org.projectlombok:lombok:1.18.24'
    compileOnly lombok
    annotationProcessor lombok
}

bukkit {
    main = 'com.nextplugins.cash.NextCash'
    authors = ['NextPlugins']
    website = 'https://github.com/NextPlugins'
    version = "${project.version}"
    softDepend = ['Citizens', 'HolographicDisplays', 'PlaceholderAPI', 'Vault', 'NextTestServer', 'CMI']
    loadBefore = ['NextUpdater']
    apiVersion = '1.13'
}

shadowJar {
    archiveName("${project.name}-${project.version}.jar")

    relocate 'com.yuhtin.updatechecker', 'com.nextplugins.cash.libs.updatechecker'
    relocate 'com.nextplugins.libs.data.converter', 'com.nextplugins.cash.libs.dataconverter'

    relocate 'com.henryfabio.minecraft.configinjector', 'com.nextplugins.cash.libs.configinjector'
    relocate 'com.henryfabio.minecraft.inventoryapi', 'com.nextplugins.cash.libs.inventoryapi'

    relocate 'com.henryfabio.sqlprovider', 'com.nextplugins.cash.libs.sqlprovider'
    relocate 'com.zaxxer.hikari', 'com.nextplugins.cash.libs.hikari'

    relocate 'me.saiintbrisson.minecraft.command', 'com.nextplugins.cash.libs.command.common'
    relocate 'me.saiintbrisson.bukkit.command', 'com.nextplugins.cash.libs.command.bukkit'

    relocate 'de.tr7zw.changeme.nbtapi', 'com.nextplugins.cash.libs.nbtapi'
    relocate 'de.tr7zw.annotations', 'com.nextplugins.cash.libs.annotations'
}

compileJava {
    options.encoding('UTF-8')

    sourceCompatibility('1.8')
    targetCompatibility('1.8')
}
